movement Common_UpOne {
    walk_up
}
movement Common_UpTwo {
    walk_up
    walk_up
}

script Common_RoutingSign {
    lock
    callnative(BufferRouteText)
    msgbox(format("{LEFT_ARROW} {STR_VAR_1} Room\n{RIGHT_ARROW} {STR_VAR_2} Room"), MSGBOX_SIGN)
    release
}

script Common_TrainerNameSign {
    lock
    callnative(BufferTrainerName)
    msgbox(format("{STR_VAR_1} {STR_VAR_2}"), MSGBOX_SIGN)
    release
}

const BALL_1_ID = 1
const BALL_2_ID = 2
const BALL_3_ID = 3

const VAR_GIFT_TAKEN = VAR_TEMP_A
const VAR_SPEC_1 = VAR_0x8001
const VAR_SPEC_2 = VAR_0x8002
const VAR_SPEC_3 = VAR_0x8003
const LEVEL = 0

script Common_GiftBall1 {
    showmonpic(VAR_SPEC_1, 10, 3)
    if (var(VAR_GIFT_TAKEN) == 1) {
       msgbox(format("It would be greedy to take another Pokémon."))
    } else {
        bufferspeciesname(STR_VAR_1, VAR_SPEC_1)
        msgbox(format("Will you take {STR_VAR_1}?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1) {
           givemon(VAR_SPEC_1, LEVEL, ITEM_NONE)
           removeobject(BALL_1_ID)
           msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
           if (var(VAR_RESULT) == 1) {
              call(Common_EventScript_GetGiftMonPartySlot)
              call(Common_EventScript_NameReceivedPartyMon)
           }
           setvar(VAR_GIFT_TAKEN, 1)
        }
    }
    hidemonpic
    releaseall
    end
}

script Common_GiftBall2 {
    showmonpic(VAR_SPEC_2, 10, 3)
    if (var(VAR_GIFT_TAKEN) == 1) {
       msgbox(format("It would be greedy to take another Pokémon."))
    } else {
        bufferspeciesname(STR_VAR_1, VAR_SPEC_2)
        msgbox(format("Will you take {STR_VAR_1}?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1) {
           givemon(VAR_SPEC_2, LEVEL, ITEM_NONE)
           removeobject(BALL_2_ID)
           msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
           if (var(VAR_RESULT) == 1) {
              call(Common_EventScript_GetGiftMonPartySlot)
              call(Common_EventScript_NameReceivedPartyMon)
           }
           setvar(VAR_GIFT_TAKEN, 1)
        }
    }
    hidemonpic
    releaseall
    end
}

script Common_GiftBall3 {
    showmonpic(VAR_SPEC_3, 10, 3)
    if (var(VAR_GIFT_TAKEN) == 1) {
       msgbox(format("It would be greedy to take another Pokémon."))
    } else {
        bufferspeciesname(STR_VAR_1, VAR_SPEC_3)
        msgbox(format("Will you take {STR_VAR_1}?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1) {
           givemon(VAR_SPEC_3, LEVEL, ITEM_NONE)
           removeobject(BALL_3_ID)
           msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
           if (var(VAR_RESULT) == 1) {
              call(Common_EventScript_GetGiftMonPartySlot)
              call(Common_EventScript_NameReceivedPartyMon)
           }
           setvar(VAR_GIFT_TAKEN, 1)
        }
    }
    hidemonpic
    releaseall
    end
}

script Common_MoveRelearner {
    lock
    faceplayer
    msgbox(format("I'm the Move Relearner! I can teach a move to a 
        Pokémon for a Heart Scale."))
    checkitem(ITEM_HEART_SCALE)
    if (var(VAR_RESULT) == TRUE) {
        msgbox(format("Looks like you have a Heart Scale! Would you like me to teach
            a move?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1) {
            while { 
                msgbox(format("Which Pokémon needs tutoring?"))
                special(ChooseMonForMoveRelearner)
                waitstate
                if (var(VAR_0x8004) == PARTY_NOTHING_CHOSEN) {
                    break
                }
                special(IsSelectedMonEgg)
                if (var(VAR_RESULT) == TRUE || var(VAR_0x8005) == 0) {
                    msgbox(format("Unfortunately, I cannot teach that Pokémon any moves."))
                    continue
                }
                msgbox(format("Which move should I teach?"))
                special(TeachMoveRelearnerMove)
                waitstate
                if (var(VAR_0x8004) == 0) {
                    continue
                }
                msgbox(format("{PLAYER} handed over one Heart Scale in exchange"))
                removeitem(ITEM_HEART_SCALE)
                break
            }
        }
    }
    msgbox(format("If your Pokémon need to learn a move, come back
        with a Heart Scale."))
    release
    end
}

script Common_GiveHeartScale {
    giveitem(ITEM_HEART_SCALE)
    setflag(FLAG_TEMP_A)
    end
}
